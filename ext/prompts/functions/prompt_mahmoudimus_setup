
# -*- mode: sh -*-
# A simple theme that displays relevant, contextual information (modified for mahmoud)
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Mahmoud Abdelkader <mahmoud@...>
#
# Screenshots:
#   http://i.imgur.com/nrGV6pg.png
#

#
# 16 Terminal Colors
# -- ---------------
#  0 black
#  1 red
#  2 green
#  3 yellow
#  4 blue
#  5 magenta
#  6 cyan
#  7 white
#  8 bright black
#  9 bright red
# 10 bright green
# 11 bright yellow
# 12 bright blue
# 13 bright magenta
# 14 bright cyan
# 15 bright white
#

# Load dependencies.
pmodload 'helper'

function prompt_mahmoudimus_async_callback {
  case $1 in
    prompt_mahmoudimus_async_git)
      # We can safely split on ':' because it isn't allowed in ref names.
      IFS=':' read _git_target _git_post_target <<<"$3"

      # The target actually contains 3 space separated possibilities, so we need to
      # make sure we grab the first one.
      _git_target=$(coalesce ${(@)${(z)_git_target}})

      if [[ -z "$_git_target" ]]; then
        # No git target detected, flush the git fragment and redisplay the prompt.
        if [[ -n "$_prompt_mahmoudimus_git" ]]; then
          _prompt_mahmoudimus_git=''
          zle && zle reset-prompt
        fi
      else
        # Git target detected, update the git fragment and redisplay the prompt.
        _prompt_mahmoudimus_git="${_git_target}${_git_post_target}"
        zle && zle reset-prompt
      fi
      ;;
    "[async]")
      # Code is 1 for corrupted worker output and 2 for dead worker.
      if [[ $2 -eq 2 ]]; then
      # Our worker died unexpectedly.
          typeset -g prompt_prezto_async_init=0
      fi
      ;;
  esac
}

function prompt_mahmoudimus_async_git {
  cd -q "$1"
  if (( $+functions[git-info] )); then
    git-info
    print ${git_info[status]}
  fi
}

function prompt_mahmoudimus_async_tasks {
  # Initialize async worker. This needs to be done here and not in
  # prompt_mahmoudimus_setup so the git formatting can be overridden by other prompts.
  if (( !${prompt_prezto_async_init:-0} )); then
    async_start_worker prompt_mahmoudimus -n
    async_register_callback prompt_mahmoudimus prompt_mahmoudimus_async_callback
    typeset -g prompt_prezto_async_init=1
  fi

  # Kill the old process of slow commands if it is still running.
  async_flush_jobs prompt_mahmoudimus

  # Compute slow commands in the background.
  async_job prompt_mahmoudimus prompt_mahmoudimus_async_git "$PWD"
}

function prompt_mahmoudimus_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  _prompt_mahmoudimus_pwd=$(prompt-pwd)

  # Handle updating git data. We also clear the git prompt data if we're in a
  # different git root now.
  if (( $+functions[git-dir] )); then
    local new_git_root="$(git-dir 2> /dev/null)"
    if [[ $new_git_root != $_mahmoudimus_cur_git_root ]]; then
      _prompt_mahmoudimus_git=''
      _mahmoudimus_cur_git_root=$new_git_root
    fi
  fi

  # Run python info (this should be fast and not require any async)
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Get Ruby info
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get Node info
  if (( $+functions[node-info] )); then
    node-info
  fi

  prompt_mahmoudimus_async_tasks
}

function ext-m-prompt-length() {
    emulate -L zsh
    local COLUMNS=${2:-$COLUMNS}
    local -i x y=$#1 m
    if (( y )); then
        while (( ${${(%):-$1%$y(l.1.0)}[-1]} )); do
            x=y
            (( y *= 2 ));
        done
        local xy
        while (( y > x + 1 )); do
            m=$(( x + (y - x) / 2 ))
            typeset ${${(%):-$1%$m(l.x.y)}[-1]}=$m
        done
    fi
    echo $x
}

# Usage: fill-line LEFT RIGHT
#
# Prints LEFT<spaces>RIGHT with enough spaces in the middle
# to fill a terminal line.
function ext-m-fill-line() {
    emulate -L zsh
    local left_len=$(ext-m-prompt-length $1)
    # echo $left_len
    local right_len=$(ext-m-prompt-length $2 999)
    # echo $right_len
    local pad_len=$((COLUMNS - left_len - right_len - ${ZLE_RPROMPT_INDENT:-1}))
    # echo $pad_len
    # echo $COLUMNS
    # if (( pad_len < 1 )); then
    #     # Not enough space for the right part. Drop it.
    #     echo -E - ${1}
    # else
        local pad=${(pl.$pad_len.. .)}  # pad_len spaces
        echo -E - ${1}${pad}${2}
    # fi
}

# this variable can be changed later to change the fraction of the line
local PROMPT_PERCENT_OF_LINE=100

# make a function, so that it can be evaluated repeatedly
function m-prompt-width() {
    echo $(( ${COLUMNS:-80} * PROMPT_PERCENT_OF_LINE / 100 ))
}

function prompt_mahmoudimus_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz async && async

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_mahmoudimus_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{1}❯%F{3}❯%F{2}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{3}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{2}❮%F{3}❮%F{1}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%F{7}:%f%%B%F{9}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{2}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{13}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{13}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format ' %%B%F{2}%b%f%%b'
  zstyle ':prezto:module:git:info:commit' format ' %%B%F{3}%.7c%f%%b'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{1}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{4}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format ' %%B%F{13}%p%f%%b'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{5}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{6}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{3}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{7}◼%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'status' '%b %p %c:%s%A%B%S%a%d%m%r%U%u'

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%f%F{1}rb:(%v)%F{7}'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '%f%F{3}py:(%v)%F{7} '

  # Set node-info parameters
  zstyle ':prezto:module:node:info:version' format '%f%F{2}node:(%v)%F{7}'

  # Set up non-zero return value display
  local show_return="✘ "
  # Default is to show the return value
  if zstyle -T ':prezto:module:prompt' show-return-val; then
      show_return+='%? '
  fi

  # Get the async worker set up.
  _mahmoudimus_cur_git_root=''

  _prompt_mahmoudimus_git=''
  _prompt_mahmoudimus_pwd=''

  # Define prompts.
  # PROMPT='%F{113} %n%f%F{7}@%f%F{3}%m:%D{%y/%m/%d,%H:%M}%f %F{9}λ%f %F{4}${_prompt_mahmoudimus_pwd}%(!. %B%F{1}#%f%b.)'
  #   PROMPT+='
  # ${editor_info[keymap]} '
  #   RPROMPT='${ruby_info[version]} ${node_info[version]}'
  #   RPROMPT+='$python_info[virtualenv]${editor_info[overwrite]}%(?:: %F{1}'
  #   RPROMPT+=${show_return}
  #   RPROMPT+='%f)${VIM:+" %B%F{6}V%f%b"}'
  #   RPROMPT+='${INSIDE_EMACS:+" %B%F{6}E%f%b"}%f'
  #   RPROMPT+=' ${_prompt_mahmoudimus_git}'
  #
  ## Multi-line prompt work:
  ##  - https://superuser.com/questions/357107/zsh-right-justify-in-ps1
  ##  - http://aperiodic.net/phil/prompt/
  local top_left='%F{113} %n%f%F{7}@%f%F{3}%m:%D{%y/%m/%d,%H:%M}%f %F{9}λ%f %F{4}${_prompt_mahmoudimus_pwd}%(!. %B%F{1}#%f%b.)'
  local top_right='${ruby_info[version]} '
  top_right+='${node_info[version]} '
  top_right+='$python_info[virtualenv] '
  top_right+='${editor_info[overwrite]}%(?:: %F{1}${show_return}%f) '
  top_right+='${VIM:+" %B%F{6}V%f%b"} '
  top_right+='${INSIDE_EMACS:+" %B%F{6}E%f%b"}%f '
  top_right+='${_prompt_mahmoudimus_git} '
  local bottom_left='${editor_info[keymap]} '
  local bottom_right=''

  # PROMPT="$(ext-m-fill-line "$top_left" "$top_right")"$'\n'$bottom_left
  width_part='$(m-prompt-width)'
  PROMPT="$top_left %${width}<..<$top_right"$'\n'$bottom_left
  RPROMPT=$bottom_right
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_mahmoudimus_preview {
    local +h PROMPT=''
    local +h RPROMPT=''
    local +h SPROMPT=''

    editor-info 2> /dev/null
    ?  prompt_preview_theme 'mahmoudimus'
}

prompt_mahmoudimus_setup "$@"
# vim: ft=zsh
